local -a gitout             # Unprocessed git output
local -a gitout2            # Processed git output
local -a nonselectables     # To be appended, upon making indices global, to ZCMDRLIST_NONSELECTABLE_ELEMENTS
local -a hop_indices        # To be appended, upon making indices global, to ZCMDRLIST_HOP_INDEXES
integer own_LINE_COUNT=0    # Local line count

# Input: number of log entries
local git_log_count="$1"

gitcmd=( git -C "$repo_root_dir" log --max-count="$git_log_count" --pretty=format:'%H%x01%s%x01%d%x01%cr%x01%an' )
gitout=( "${(@f)"$( "${gitcmd[@]}" )"}" )

gitout2=( "${CMDR_RED}'Current HEAD Git commits of ${CMDR_RED_E}${CMDR_YELLOW}${(q)repo_root_dir:t}${CMDR_YELLOW_E}${CMDR_RED}'${CMDR_RED_E}" )
own_LINE_COUNT+=1
nonselectables+=( "$own_LINE_COUNT" )
hop_indices+=( "$own_LINE_COUNT" )

local IFSBKP="$IFS"
local IFS=$'\1'
local entry entry3
local -a arrentry
integer ecount=0
for entry in "${gitout[@]}"; do
    # Count entries
    ecount+=1

    # hash \1 message \1 repo info \1 date \1 author
    arrentry=( ${=entry} )
    entry3="${arrentry[3]## }"
    entry3="${(q-)entry3//\([[:space:]]#HEAD[[:space:]]#->[^,]#,[[:space:]]#/(}"
    [[ -n "$entry3" && "$entry3" != "''" ]] && entry3=" ${CMDR_YELLOW}$entry3${CMDR_YELLOW_E}" || entry3=""

    reply=( )
    local txt1="${arrentry[1]}"
    txt1="${CMDR_GREEN}${txt1[1,7]}${CMDR_GREEN_E}"
    local txt2="${CMDR_CYAN}${(q-)arrentry[2]}${CMDR_CYAN_E}"
    __zcmdr_stdlib_get_std_button "glog1A" "0" "/Same-files commits/" "$repo_root_dir" "${arrentry[1]}" "$txt1" __zcmdr_action_git_same_files
    __zcmdr_stdlib_get_std_button "glog1B" "0" "/Exactly-files commits/" "$repo_root_dir" "${arrentry[1]}" "$txt2" __zcmdr_action_git_same_files_e

    # Before adding new entry - check if a separator is needed
    if (( ecount > ZCMDR[LOG_SIZE_INITIAL] )); then
        integer extending_entries=$(( ecount - ZCMDR[LOG_SIZE_INITIAL] ))
        if (( (extending_entries-1) % 5 == 0 )); then
            gitout2+=( "${CMDR_RED}---${CMDR_RED_E}" )
            own_LINE_COUNT+=1
        fi
    fi

    gitout2+=( "${reply[1]} ${reply[2]}$entry3 ${CMDR_MAGENTA}((${arrentry[4]}))${CMDR_MAGENTA_E} \`${arrentry[5]}\`" )
    own_LINE_COUNT+=1
done
IFS="$IFSBKP"

# Add "More+5" and "Less-5" buttons to fetch more and less log entries
reply=( )

__zcmdr_stdlib_get_button "" "regenerate" "log" "plus" "" "More+5" ""
if [[ "$own_LINE_COUNT" -gt 7 ]]; then
    __zcmdr_stdlib_get_button "" "regenerate" "log" "minus" "" "Less-5" ""
    reply[2]=" ${reply[2]}"
fi

gitout2+=( "${reply[1]}${reply[2]}" )
own_LINE_COUNT+=1

reply=( "${gitout2[@]}" )
reply2="$own_LINE_COUNT"
reply3=( "${nonselectables[@]}" )
reply4=( "${hop_indices[@]}" )

# vim:ft=zsh
